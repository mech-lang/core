# Bouncing Balls

Set up the environment
  #ball += nx4[x y vx vy] = 0
  #system/timer += [#timer resolution: 60Hz]
  #gravity = 9.8m/s^2

## Update Condition

The positions of the balls updates on every tick
  on #timer.tick
  [#ball x y vx vy]
  x := x + vx
  y := y + vy
  vy := vy + #gravity * #timer.dt

## Boundary Conditions

Constrain balls to within the boundary, reverse 
  [#ball x y vx vy]
  boundary = 5km
  ix = x > boundary
  iy = y > boundary
  i0 = x < 0km
  x[ix] := boundary
  y[iy] := boundary
  x[i0] := 0
  vy[iy] := -vy * 90%
  vx[ix or i0] := -vx * 90%

## Export balls

  [#ball x y]
  ws = #[client/websocket]
  ws.send += x
  ws.send += y

## Reset on click

  on click = [#html/event/click]
  [#ball x y]
  x := click.x
  y := click.y